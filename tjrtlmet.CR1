'this version running on station beginning on 6/23/2008
'Version 5.6  last modified feb 27,2013 by mcordrey
'  maxmin subs chagned to fix SWMP table timestamp formatting

'Version 5.5 changes, Search for '*** to display these additions
'Added 24 hour rain total (DailyRain) to SWMP table
'Applied a new Field Name for Daily Rain as CumPrcp
'Made GOESDailyRain = DailyRain*1000 output for transmission
'Added Variable GOESDailyRain & GoesPar to public
'Set Units for GOESDailyRain and DailyRain to mm
'Set Units for GOESPar to mmoles/m^2
'Applied a new Field Name for GOESDaily Rain as G_CumPrcp
'Reset DailyRain to Zero at midnight
'Reset GOESDailyRain to Zero at midnight
'Deleted the  If Par < 0 then Par is 0 line as per DMC 2007
'Changed "Day" to "Days" in Alias time(3) due to conflict with new CR1000 Firmware

'Version 4.0 changes:
'Last Modified: June 21st, 2007 by Jay Poucher
' Add check on wind speed. If NAN, don't include sample is processing for
' wind vector instruction. Reason: clock set function causing 1 bad wind sample per day
' which causes one bad wind summery per day. Added variable WindGood as boolean, set true
' or false after wind speed pulse read in struction. WindGood is used as the process
' disable variable in the Windvector() function. Search for MOD** to see changes

'Version 3.0 changes
' 08/30/2006 
'		Changes made as requested by the Data Management Committee:
'		- fiftnmin table name changed to SWMP
'		- Relative humidity min/max, pressure min/max, wind speed min and time fields were dropped
'		- fieldnames instructions added to SWMP data table to generate consistent with cr10x/CDMO fieldnames
'		- RHumidity > 100 and PAR < 0 instructions dropped, over or under values will be handled post processing
'		- subroutine now used to generate min/max values and timestamps for both SWMP and GOESOUT tables 
'		Additonal changes made:
'		- revision of variable names within program for consistency and improved readability
'		- Units instructions were added to enable output of parameter units in data table headers 
'				when using TOA5 long header ASCII download format
' 09/12/2006
'		- Added colon to max/min timestamps in SWMP table
'		- Added fieldnames instructions to GOES table (GOES field names same as SWMP with 'G_' prepended)

'NERRS Site: Tijuana River
'Location: Imperial Beach, CA
'Site: Tidal Linkage
'NESDIS Assignments:
'ID=3B01468A
'Transmit Time (mm:ss after the hour) = 47:00
'ST MsgWindow = 10 sec
'ST Channel = 152
'ST BPS = 300
'Random Channel = 0
'Random BPS = 0
'ST Repeat Interval = 1 hour
'ST Offset = 47 minute, 0 secs  
'Random Repeat Interval = 0 hour

'Active Sensor list:
'HMP45C (7-wire) Temperature & Relative Humidity (se1 se2 c1)
'Campbell CS105 (PTB101B) Barometric Pressure Sensor (se3)
'Campbell 5103 Wind Speed & Direction Sensor (p1 se4 ex1)
'Texas Electronics TE-525 Rain Gauge (p2)
'LI190SB Quantum Sensor (diff3 == se5 se6)  

'Holds Sensor Calibration Settings for Easy Access in the Numeric Display
PreserveVariables

'For Easy Site Identification in the Data Header (Parseable)
StationName (tjrtlmet)

'Time offset for GMT time
'http://wwp.greenwichmeantime.com/
Const GMTOFFSET=8

'par multiplier setting:
'The procedure to calculate the correct multiplier for mmoles/m^2 for licor sensor: 
'[1/(LI-COR calibration number * 0.604)] * 5 

'from ???  til april 23, 2010
''Const PARMULTIPLIER = 1.2581  'q40041  cal 7/12/2008
''Const PAR2MULTIPLIER = 1.33  'q33685 cal 12/04/2008
'Const PARMULTIPLIER = 1.33  'q33685 removed
'Const PAR2MULTIPLIER = 1.2581  'q40041

'LI-190SB  serial: q48068
'cal constant: 6.25  June 28,2012
Const PARMULTIPLIER = 1.325   

'apogee serial 12144  cal factor = 5.00
'set to 1 to get raw mv readings
Const APOGEEMULTIPLIER = 1

'GOES constants  TIMETOTRANSMITTER needs to be atleast 2min before TRANSMITTIME
''const TRANSMITTIME = "0_47_0"  'Time after hour transmitter will send to GOES ("00_MM_SS")
''const TIMETOTRANSMITTER = 45		'minutes after the hour data sent from cr1000 to transmitter

'Declare Public Variables
'  Parameters directly sampled by sensors
Public AirTemp, RHumidity, Pressure, WindSpd, WindDir, Precip, PAR, Batt, WindGood As Boolean'MOD**
'TotalRad, totRadMultiplier 		'uncomment if total radiation sensor installed (remember to add to tables)
Public pTempC, DailyRain '***
'Public EppleyPyr, eppMultiplier 'uncomment if eppley pyrometer is installed
'EnclosureRH 	     'uncomment if relative humidity sensor installed in enclosure

' GOES output variables 
Public goesSetResult,goesFailures,gpsLoc(6),gpsTime(7), lastStats(14)

' Variables used in max and min subroutine and to calculate timestamps 
Public atMax,atMaxTime,atMin,atMinTime,wsMax,wsMaxTime
Public atMaxTimeSwmp as string,atMinTimeSwmp as string,wsMaxTimeSwmp as string

' Variables used for integer translations in the GOES Output
public GoesAT,goesATMax,goesATMin,goesRH,goesBP,goesWS,goesWD,goesWSD,goesWSMax,goesPar,goesPrecip,goesBatt,goesDailyRain '***

Public temp2, rh2, PARApogee

Public Time(9) 'array to store time

Alias Time(1) = Year
Alias Time(2) = Month
Alias Time(3) = Days
Alias Time(4) = Hour
Alias Time(5) = Minute
Alias Time(6) = Second
Alias Time(7) = Microsecond
Alias Time(8) = DayofWeek
Alias Time(9) = DayofYear

Alias GPSTime(1) = gYear
Alias GPSTime(2) = gMonth
Alias GPSTime(3) = gDay
Alias GPSTime(4) = gHour
Alias GPSTime(5) = gMinute
Alias GPSTime(6) = gSecond
Alias GPSTime(7) = gMicroSecond   

Alias GPSLoc(1) = gpsResult 	'GOESgps instruction result code
Alias GPSLoc(2) = gpsSeconds 	'seconds since Jan 1, 2000
Alias GPSLoc(3) = gpsLat  		'Fractional degrees; 100 nanodegree resolution
Alias GPSLoc(4) = gpsLon			'Fractional degrees; 100 nanodegree resolution
Alias GPSLoc(5) = gpsElev  		'Signed 32-bit number, in centimeters
Alias GPSLoc(6) = gpsMagElev  'Fractional degrees; 1 millidegree resolution 

Alias lastStats(4) = fwdPower
Alias lastStats(5) = reflPower
Alias lastStats(6) = loadVolts
Alias lastStats(7) = gpsAqTime

'Define units for SWMP data table output
Units AirTemp = C
Units atMax = C
Units atMin = C
Units RHumidity = %
Units Pressure = mb
Units WindSpd = m/s
Units WindDir = deg
Units	Precip = mm
Units PAR = mmoles/m^2
Units Batt = Volts
Units DailyRain = mm '***

'Define units of max/min timestamps used in SWMP table
Units atMaxTimeSwmp = hhmm
Units atMinTimeSwmp = hhmm 
Units wsMaxTimeSwmp = hhmm
'Define units of max/min timestamps used in GOES table
Units atMaxTime = hhmm
Units atMinTime = hhmm  
Units wsMaxTime = hhmm

'Define units for GOES data table output
Units goesAT = C
Units goesATMax = C
Units goesATMin = C
Units goesRH = %
Units goesBP = mb
Units goesWS = m/s
Units goesWD = Deg
Units goesWSD = m/s
Units goesWSMax = m/s
Units goesPrecip = mm
Units goesBatt = Volts
Units goesDailyRain = mm '***
Units goesPar = mmoles/m^2 '***

'''Define Data Tables:
' The SWMP data table definition:
DataTable (SWMP,1,-1)
	DataInterval (0,15,min,10)
	Average (1,AirTemp,IEEE4,False)
	Fieldnames("ATemp") 'instruction is used to output desired fieldnames in output table headers
	Sample (1,atMax,IEEE4)
	Fieldnames("MaxTemp")
	Sample (1,atMaxTimeSwmp,String)
	Fieldnames ("MaxTempT")
	Sample (1,atMin,IEEE4)
	Fieldnames("MinTemp")
	Sample (1,atMinTimeSwmp,String)
	Fieldnames("MinTempT")
	Average (1,RHumidity,IEEE4,False)
	Fieldnames("RH")
	Average (1,Pressure,IEEE4,False)
	Fieldnames("BP")
	WindVector (1,WindSpd,WindDir,IEEE4,WindGood,0,0,0)'MOD**
	Fieldnames("WSpd,Wdir,SDWDir")
	Sample (1,wsMax,IEEE4)
	Fieldnames("MaxWSpd")
	Sample (1,wsMaxTimeSwmp,String)
	Fieldnames("MaxWSpdT")
	Totalize (1,Precip,IEEE4,False)
	Fieldnames("TotPrcp")
	Totalize(1,PAR,IEEE4,False)'read from secondary par input while recalibrating primary
	FieldNames("TotPAR")  
	Average (1,Batt,IEEE4,False)
	Fieldnames("AvgVolt")
	Sample(1,DailyRain,IEEE4) '***
	Fieldnames("CumPrcp") '***
EndTable

' The Goes Output Data Table Def
DataTable (GOESOUT,1,-1)
	DataInterval (0,15,min,10)
	Average(1,goesAT,IEEE4,False)
	Fieldnames("G_ATemp")
	Sample(1,goesATMax,IEEE4)
	Fieldnames("G_MaxTemp")
	Sample(1,atMaxTime,IEEE4)
	Fieldnames("G_MaxTempT")
	Sample(1,goesATMin,IEEE4)
	Fieldnames("G_MinTemp")
	Sample(1,atMinTime,IEEE4)
	Fieldnames("G_MinTempT")
	Average (1,goesRH,IEEE4,False)
	Fieldnames("G_RH")
	Average (1,goesBP,IEEE4,False)
	FieldNames("G_BP")
	Sample (1,goesWS,IEEE4)
	Fieldnames("G_WSpd")
	Sample (1,goesWD,IEEE4)
	Fieldnames("G_Wdir")
	Sample (1,goesWSD,IEEE4)
	Fieldnames("G_SDWDir")
	Sample(1,goesWSMax,IEEE4)
	Fieldnames("G_MaxWSpd")
	Sample(1,wsMaxTime,IEEE4)
	Fieldnames("G_MaxWSpdT")
	Totalize (1,goesPrecip,IEEE4,False)
	Fieldnames("G_TotPrcp")
	Totalize (1,goesPAR,IEEE4,False) 'reading from par2!!!
	FieldNames("G_TotPAR")  
	Average (1,goesBatt,IEEE4,False)
	Fieldnames("G_AvgVolt")
	Sample(1,GOESDailyRain,IEEE4) '***
	Fieldnames("G_CumPrcp") '***
EndTable

' Table used to record trouble shooting data.
' All three values should be zero
DataTable(GoesMetrics,true,300)
  Sample(1,goesFailures,fp2)
  Sample(1,gpsLoc(1),fp2)
  Sample(1,goesSetResult,fp2)
  Sample(1,fwdPower,fp2)
  Sample(1,reflPower,fp2)
  Sample(1,loadVolts,fp2)
  Sample(1,gpsAqTime,fp2)
EndTable

DataTable (TRNERR, 1,-1)
	DataInterval (0,15,min,10)
	Sample (1,PAR,IEEE4)
	average(1,PAR,ieee4,false)
	Totalize(1,PAR,IEEE4,False)
	FieldNames("TotPAR")
	Sample(1,PARApogee,IEEE4)
	average(1,PARApogee,ieee4,false)
	Totalize(1,PARApogee,IEEE4,False)
	FieldNames("TotPARAp")
	
	'average(1,AirTemp,IEEE4,False)
	'average(1,temp2,IEEE4,False)
	
	'average(1,RHumidity,IEEE4,False)
	'average(1,rh2,IEEE4,False)
	
EndTable	

Sub MaxMin	' calculated max/min and timestamps for SWMP table (goes integer values are derived from)
	if AirTemp < atMin then 	'Track Min and time of Min air temp
		atMin = AirTemp
		Realtime(time())
		atMinTime = Hour*100+Minute 'time formatted as integer for GOES table
		If Minute < 10
		  atMinTimeSwmp = Hour + ":0" + Minute  'time formatted as string for SWMP table
		Else
		  atMinTimeSwmp = Hour + ":" + Minute
		EndIf
	EndIf
	If AirTemp > atMax then	'Track Max and time of Max air temp
		atMax = AirTemp
		Realtime(time())
		atMaxTime = Hour*100+Minute
		If Minute < 10
		  atMaxTimeSwmp = Hour + ":0" + Minute
		Else
		  atMaxTimeSwmp = Hour + ":" + Minute
		EndIf
	EndIf
	If WindSpd > wsMax then	'Track Max and time of max wind
		wsMax = WindSpd
		Realtime(time())
		wsMaxTime = Hour*100+Minute
		If Minute < 10
		  wsMaxTimeSwmp = Hour + ":0" + Minute
		Else
		  wsMaxTimeSwmp = Hour + ":" + Minute
		EndIf
	EndIf
endsub

'The setup for the GOES TX312 transmitter
Sub setup
	GOESSetup (goesSetResult,&H3B01468A,10,152,300,0,100,"0_1_0_0","0_47_0","0_0_0")
endsub

'Main Program
BeginProg
	'Initialize Min & Max Variables
	atMin = 20000
	atMax = -20000
	wsMax = -20000
	'initialize so goes setup subroutine will be called on first run of program
	goesSetResult = -1
	'Scan Rate
	Scan (5,Sec,0,0)

'''Begin sensor settings

		'Reference Temperature and Battery Voltage measurements
		'"PTempC" in C and "Batt" in Volts:
		PanelTemp (PTempC,250)
		Battery (Batt)

		'HMP45C (7-wire switched mode) Temperature & Relative Humidity Sensor measurements
		'"AirTemp" in C & "RHumidity" in %:
		'portset(1,1)
		'Delay(0,150,mSec)
		'VoltSE(AirTemp,1,mV2500,1,0,0,_60Hz,0.1,-40.0)
		'VoltSE(RHumidity,1,mV2500,2,0,0,_60Hz,0.1,0)
		
		'HC2S3 Temp & Rel Hum sensor
		portset(9,1)
		Delay(0,3,sec)
		VoltSE(AirTemp,1,mV2500,1,0,0,_60Hz,0.1,-40.0)
		VoltSE(RHumidity,1,mV2500,2,0,0,_60Hz,0.1,0)
		portset(9,0)

		'CS105 (continuous power mode) Barometric Pressure Sensor measurement
		'"Pressure" in mb
		''VoltSE(Pressure,1,mV2500,3,1,0,_60Hz,0.184,600.0)
		
		'cs106  (installed 05/19/2014)
		VoltSE(Pressure,1,mV2500,3,1,0,_60Hz,0.240,500)
		
		'05103 Wind Speed & Direction Sensor measurements 
		'"WindSpd" in mps and "WindDir" in degrees:
		PulseCount(WindSpd,1,1,1,1,0.098,0)
		If WindSpd = NAN Then'MOD**
			WindGood = true'MOD**
		Else'MOD**
			WindGood = false'MOD**
		EndIf'MOD**
		BrHalf(WindDir,1,mV2500,4,1,1,2500,True,0,_60Hz,355,0)
		If windDir>=360 Then windDir=0
		
		'03002 Wind Speed & Direction Sensor measurements 
		'"WindSpd" in mps and "WindDir" in degrees:
'		PulseCount(WindSpd,1,1,1,1,0.75,0.2)
'		If WindSpd<.21 Then WindSpd=0
'		If WindSpd = NAN Then'MOD**
'			WindGood = true'MOD**
'		else'MOD**
'			WindGood = false'MOD**
'		endif'MOD**
'		BrHalf(WindDir,1,mV2500,4,1,1,2500,True,0,_60Hz,352,0)
'		If windDir>=360 Then windDir=0

		'Texas Electronics TR-525I Rain Gauge measurement
		'"Precip" in mm:
		PulseCount(Precip,1,2,2,0,0.254,0)	
		DailyRain = DailyRain+Precip '***

		'LI190SB Quantum Sensor measurements
		'"PAR" in mmoles/m^2:
		VoltDiff(Par,1,mV25,3,True,0,_60Hz,PARMULTIPLIER,0)
		
		''test sensors:
		'Apogee SQ-110 PAR sensor
		'400mv max output??
		VoltDiff(PARApogee,1,mV2500,6,True,0,_60Hz,APOGEEMULTIPLIER,0)
		''PARApogee = PARApogee * 5
		
		'li190sb
		'VoltDiff(par2,1,mV25,6,true,0,_60Hz,PAR2MULTIPLIER,0)
		'PortSet(2,1)
		'Delay(0,150,mSec)
		'voltse(temp2,1,mv2500,9,0,0,_60hz,0.1,-40)
		'voltse(rh2,1,mv2500,10,0,0,_60hz,0.1,0)
		'portset(2,0)
		
'''End sensor setttings 

		'Reset station clock using GPS clock
		If TimeintoInterval (722,1440,min) then	'at two minutes past noon set clock to gps clock
			GOESGPS (gpsloc(),gpsTime())
			if gpsResult=0and gHour<24and gHour>=0and gMinute<60and gMinute>=0and gSecond<60and gSecond>=0 then
				gHour = gHour - GMTOFFSET 'adjust clock in GMT to local time
				if gHour<0 then
					gHour=gHour+24
				endif
				ClockSet (GPSTime())
			Endif
		endif
		
		call MaxMin		'track airTempMax, airTempMin, wsMax and times
		
		CallTable SWMP
		CallTable TRNERR
		
		'Convert and store as integers for GOES transmission (only those not already integers)
		goesBatt = Batt * 100
		goesAT = AirTemp * 100
		goesATMAx = atMax * 100
		goesATMin = atMin * 100
		goesRH = RHumidity * 10
		goesBP = Pressure * 10
		goesPrecip = Precip * 1000
		goesPar = Par
		goesWSMax = wsMax * 100
		goesDailyRain = DailyRain*1000 '***
		
		'following must be called only after Fiftnmin table written to because 
		'   values are generated by WindVector instruction
		goesWS = SWMP.WSpd*100
		goesWD = SWMP.Wdir*10
		goesWSD = SWMP.SDWDir*10

		CallTable GOESOUT
		
		'GOES Instructions
		'		send to transmitter at time atleast 2 minutes before transmit time
		If TimeintoInterval (45,60,Min) then
			GOESData (GOESFailures,GOESOut,0,1,2)
		endif
		
		'Run setup subroutine if GOESSetResult is anything other than 0
		if GOESSetResult then call setup

		if TimeintoInterval(0,15,min) then	'After Min and Max are written by MaxMin sub
			atMin = 20000		'reset initial conditions for temp min and max and wind spd max
			atMax = -20000
			wsMax = -20000
		endif

		'reset all daily rain values to 0 every 24 hours
		if iftime(0,1440,min) then DailyRain = 0 '***
		if iftime(0,1440,min) then goesDailyRain = 0 '***
		
		If IfTime(0,60,min) Then
			GoesStatus(lastStats(),2) 'get the status of last transmission
      CallTable GoesMetrics
    EndIf

	NextScan
EndProg

